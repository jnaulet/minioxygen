#!/usr/bin/env perl
use strict;
use warnings;

use FindBin 1.51 qw( $RealBin );
use lib $RealBin;

package Main;

use MiniOxygen::Source;
use MiniOxygen::Render;
use MiniOxygen::Markdown;

use Carp;
use Getopt::Long qw(GetOptions);
use Data::Dumper;

# Version (required by perlcritic --brutal)
our $VERSION = '0.1a';

sub main {

    my %db;
    my $lang = 'any';

    # parse options
    GetOptions( 'lang|l=s' => \$lang )
      or croak "Usage: minioxyen [--lang|l language] file(s)...\n";

    while ( my $arg = shift @ARGV ) {

        my $file   = undef;
        my $source = MiniOxygen::Source->new( $arg, lc $lang );

        while ( my $token = $source->next_token() ) {

            # new token
            if ( defined $token->{file} ) {
                $file = $token->{file};

                # $db{$file} = $token;
            }

            if ( defined $file ) {
                my $type = $token->{type};
                push @{ $db{$file}->{$type} }, $token;
            }
        }
    }

    # Now render this db
    MiniOxygen::Render::list_files( \%db );

    foreach my $file ( keys %db ) {

        # file details
        MiniOxygen::Render::file( $db{$file}->{file}[0] );

        # file definitions
        MiniOxygen::Render::list_defs( $db{$file}->{def} );

        # file data types
        MiniOxygen::Render::list_enums( $db{$file}->{enum} );

        # file data structures
        MiniOxygen::Render::list_structs( $db{$file}->{struct} );

        # file functions
        MiniOxygen::Render::list_functions( $db{$file}->{function} );

        # separator
        MiniOxygen::Markdown::line();

        # details definitions
        for my $def ( @{ $db{$file}->{def} } ) {
            MiniOxygen::Render::def( $def, $file );
        }

        # data types definitions
        for my $enum ( @{ $db{$file}->{enum} } ) {
            MiniOxygen::Render::enum( $enum, $file );
        }

        # data structs definitions
        for my $struct ( @{ $db{$file}->{struct} } ) {
            MiniOxygen::Render::struct( $struct, $file );
        }

        # functions definitions
        for my $func ( @{ $db{$file}->{function} } ) {
            MiniOxygen::Render::function( $func, $file );
        }
    }

    return;
}

# run
main;
